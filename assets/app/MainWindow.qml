import QtQuick
import "qrc:/component/control"
import "qrc:/js/color.mjs" as Color

Window {
    id: root
    visible: true
    width: 1280
    height: 720
    title: 'Ping'

    /* Status Bar */
    Rectangle {
        id: statusBar
        anchors {
            top: parent.top;
            right: parent.right;
            left: parent.left
        }
        height: 45
        color: Color.mantle

        /* Move the window by dragging the status bar */
        MouseArea {
            anchors.fill: parent
            cursorShape: Qt.OpenHandCursor

            property int clickPosX
            property int clickPosY

            /* Save the cursor position on the status bar */
            onPressed: (mouse) => {
                this.clickPosX = mouse.x;
                this.clickPosY = mouse.y;
            }

            /* Move the window */
            onPositionChanged: (mouse) => {
                root.x += mouse.x - this.clickPosX;
                root.y += mouse.y - this.clickPosY;
            }
        }

        /* Action Buttons */
        component ActionButton: Rectangle {
            height: parent.height
            width: parent.height * 2
            property alias source: image.source

            /* Default and hover colors */
            color: "transparent"
            property string hoverColor: Color.grey_05

            /* OnClicked signal */
            signal clicked()

            Image {
                id: image
                anchors.centerIn: parent
                fillMode: Image.PreserveAspectFit
            }

            MouseArea {
                /* Fill the parent */
                anchors.fill: parent

                /* Change the background color on mouse over */
                hoverEnabled: true
                onEntered: parent.color = parent.hoverColor
                onExited: parent.color = "transparent"

                /* Change the cursors type to pointer */
                cursorShape: Qt.PointingHandCursor

                /* Send the onClicked signal */
                onClicked: parent.clicked();
            }
        }
        ActionButton {
            id: collapseButton
            anchors.right: maximizeButton.left
            anchors.top: parent.top
            source: 'qrc:/img/status-bar/collapse.svg'
        }
        ActionButton {
            id: maximizeButton
            anchors.right: closeButton.left
            anchors.top: parent.top
            source: 'qrc:/img/status-bar/maximize.svg'
        }
        ActionButton {
            id: closeButton
            hoverColor: Color.red
            anchors.right: parent.right
            anchors.top: parent.top
            source: 'qrc:/img/status-bar/close.svg'
        }
    }

    /* Chat List */
    Rectangle {
        id: chatList
        anchors {
            top: statusBar.bottom
            bottom: parent.bottom
            left: parent.left
        }
        width: 500
        color: Color.mantle

        property real minWidth: 75

        /**
         * Resize the chat list
         * @param newWidth A new chat list width
         */
        function resize(newWidth = 0): void {
            if (newWidth !== 0 && (newWidth > 300 || newWidth < this.minWidth))
                this.width = newWidth;
            if (messageArea.width < 350) this.width = root.width - 350;
            if (this.width < 250) this.width = this.minWidth;
        }

        /* Resizer */
        MouseArea {
            height: parent.height
            width: 20
            cursorShape: Qt.SizeHorCursor
            anchors.horizontalCenter: parent.right
            anchors.top: parent.top

            /* Resize the chat list */
            onPositionChanged: (mouse) => {
                parent.resize(parent.width + mouse.x - this.width / 2);
            }
        }
    }

    /* Message Area */
    Rectangle {
        id: messageArea
        anchors {
            top: statusBar.bottom
            right: parent.right
            bottom: parent.bottom
            left: chatList.right
        }
        color: Color.base
    }

    /* Resize the chat list */
    onWidthChanged: chatList.resize();
}